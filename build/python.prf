# profile to include and link Python

# Change this variable to your python version (2.6, 2.7, 3.3, ...)
msvc:PYTHON_VERSION=27
unix:PYTHON_VERSION=2.7
mingw:PYTHON_VERSION=2.7

macx {
  # for macx you need to have the Python development kit installed as framework
  INCLUDEPATH += /System/Library/Frameworks/Python.framework/Headers
  LIBS += -F/System/Library/Frameworks -framework Python
} else:msvc {
  # for windows install a Python development kit or build Python yourself from the sources
  # Make sure that you set the environment variable PYTHON_PATH to point to your
  # python installation (or the python sources/header files when building from source).
  # Make sure that you set the environment variable PYTHON_LIB to point to
  # the directory where the python libs are located.
  #
  # When using the prebuild Python installer, this will be:
  # set PYTHON_PATH = c:\Python26
  # set PYTHON_LIB  = c:\Python26\libs
  #
  # When using the python sources, this will be something like:
  # set PYTHON_PATH = c:\yourDir\Python-2.6.1\
  # set PYTHON_LIB  = c:\yourDir\Python-2.6.1\PCbuild8\Win32

  # check if debug or release
  CONFIG(debug, debug|release) {
    DEBUG_EXT = _d 
  } else {
    DEBUG_EXT = 
  }

  INCLUDEPATH += $$(PYTHON_PATH)/PC $$(PYTHON_PATH)/include
  LIBS += $$(PYTHON_LIB)/python$${PYTHON_VERSION}$${DEBUG_EXT}.lib
} else:mingw {
  LIBS += $$system(mingw32-pkg-config --libs python)
  QMAKE_CXXFLAGS += $$system(mingw32-pkg-config --cflags python)
} else:unix {
  # on linux, python-config is used to autodetect Python.
  # make sure that you have installed a matching python-dev package.
  
  LIBS += $$system(python$${PYTHON_VERSION}-config --libs)
  QMAKE_CXXFLAGS += $$system(python$${PYTHON_VERSION}-config --includes)
}
